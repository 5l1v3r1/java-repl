plugins {
    id "com.github.johnrengelman.shadow" version "1.2.4"
    id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'java'
apply plugin: 'maven-publish'


def projectVersion = version

repositories {
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2" }
    maven { url "http://repo.bodar.com" }
}

publishing {
    repositories {
        jcenter()
    }
    publications {
        shadow(MavenPublication) {
            groupId 'org.javarepl'
            artifactId 'javarepl'
            from components.shadow
        }
    }
}

bintray {
    user = System.getenv("BINTRAY_USERNAME")
    key = System.getenv("BINTRAY_API_KEY")
    publications = ['shadow']
    publish = true

    pkg {
        repo = 'maven'
        name = 'javarepl'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/albertlatacz/java-repl.git'

        version {
            name = projectVersion
            desc = 'Auto-releasing Java REPL v.' + projectVersion +'!'
            vcsTag = projectVersion
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }

        resources {
            srcDirs = ['src']
        }

    }

    test {
        java {
            srcDirs = ['test']
        }
    }
}


shadowJar {
    baseName = "javarepl"
    classifier = null

    relocate 'com.googlecode.totallylazy', 'javarepl.internal.totallylazy'
    relocate 'com.googlecode.lazyparsec', 'javarepl.internal.lazyparsec'
    relocate 'com.googlecode.utterlyidle', 'javarepl.internal.utterlyidle'
    relocate 'com.googlecode.yadic', 'javarepl.internal.yadic'
    relocate 'jline', 'javarepl.internal.jline'


}

jar {
    manifest {
        attributes 'Main-Class': 'javarepl.Main'
        attributes 'Specification-Title': 'java-repl'
        attributes 'Specification-Version': version
        attributes 'Specification-Vendor': 'Albert Latacz'
        attributes 'Implementation-Title': 'java-repl'
        attributes 'Implementation-Version': version
        attributes 'Implementation-Vendor': 'Albert Latacz'
    }
}

dependencies {
    compile "com.googlecode.totallylazy:totallylazy:2.249"
    compile "com.googlecode.utterlyidle:utterlyidle:2.108"
    compile "com.googlecode.yadic:yadic:2.48"
    compile "jline:jline:2.14.2"

    testCompile "org.hamcrest:hamcrest-core:1.3"
    testCompile "org.hamcrest:hamcrest-library:1.3"
    testCompile "junit:junit-dep:4.8.2"
}







